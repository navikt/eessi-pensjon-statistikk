buildscript {
    ext {
        springBootVersion = '2.7.5'
        springKafkaVersion = '2.9.2'
        logstashLogbackVersion = '7.2'
        archunitVersion = '1.0.1'
        tokenSupportVersion = '2.1.8'
        epMetricsVersion = "1.0.1"
        epeuxVersion = '1.3.31'
        epLoggingVersion = '1.1.6'
    }

    repositories {
        maven {
            url "https://maven.pkg.github.com/navikt/maven-release"
            credentials {
                username = "token"
                password = "$System.env.GITHUB_TOKEN"
            }
        }
    }
}

plugins {
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "1.1.0"
}

apply from: "buildSrc/ep-module.gradle"

group = 'no.nav.eessi.pensjon'

dependencies {
    //eessi pensjon libs
    implementation("no.nav.eessi.pensjon:ep-logging:${epLoggingVersion}")
    implementation("no.nav.eessi.pensjon:ep-eux:${epeuxVersion}") {
        exclude module: 'ep-security-sts'
    }
    implementation("no.nav.eessi.pensjon:ep-metrics:$epMetricsVersion")

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.yaml:snakeyaml:1.33") // Pga https://security.snyk.io/package/maven/org.yaml:snakeyaml
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-actuator")

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:5.3.23"))

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.14.0'

    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation "org.springframework.kafka:spring-kafka:$springKafkaVersion"

    //GCP
    implementation(platform("com.google.cloud:libraries-bom:26.1.5"))
    implementation("com.google.cloud:google-cloud-storage")

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

    //Auth
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")

    implementation("no.nav.security:token-validation-test-support:2.0.5")

    //test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: "junit-vintage-engine"
    }
    testImplementation('io.mockk:mockk:1.13.2')
    testImplementation("com.ninja-squad:springmockk:3.1.2")

    testImplementation "org.springframework.kafka:spring-kafka-test:$springKafkaVersion"
    testImplementation(platform("org.junit:junit-bom:5.9.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.mock-server:mockserver-netty:5.14.0") {
        exclude module: 'junit'
    }

    // Architecture tests
    testImplementation ("com.tngtech.archunit:archunit:${archunitVersion}")

}

test {
    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"

    // set JVM arguments for the test JVM(s)
    failFast = true
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}
